plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose' version '2.2.0'
    id 'com.google.gms.google-services'
    id "com.google.devtools.ksp"
    id "org.sonarqube" version "4.2.1.3168"
    id 'jacoco'
}

def getPropertiesFile = { path ->
    Properties properties = new Properties()
    def propertiesFile = rootProject.file(path)
    if (propertiesFile.exists()) {
        properties.load(new FileInputStream(propertiesFile))
    }
    return properties
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace 'com.xpeho.xpeapp'
    compileSdk 36

    ndkVersion '28.2.13676358'

    defaultConfig {
        applicationId "com.xpeho.xpeapp"
        minSdk 26
        targetSdk 36
        versionCode project.hasProperty('versionCode') ? project.versionCode.toInteger() : 1
        versionName project.hasProperty('versionName') ? project.versionName : "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = false
        }
    }

    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            getPropertiesFile('app/config/uat.properties').each { p ->
                buildConfigField 'String', p.key, p.value
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            getPropertiesFile('app/config/prod.properties').each { p ->
                buildConfigField 'String', p.key, p.value
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
        buildConfig = true // déplacé depuis gradle.properties
    }
    composeOptions {
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

jacoco {
    toolVersion = "0.8.7"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

task jacocoTestReport(type: JacocoReport) {
    dependsOn 'testDebugUnitTest' // Run unit tests before generating the report

    reports {
        xml.required.set(true)
        html.required.set(true)
    }

    def fileFilter = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            '**/ui/**',
            '**/AppModule.kt',
            '**/MainActivity.kt',
            '**/XpeApp.kt',
            '**/DatastorePref.kt',
            '**/FirebaseService.kt',
            '**/WordpressService.kt',
            '**/*Interceptor.kt',
    ]
    def debugTree = fileTree(dir: layout.buildDirectory.dir("tmp/kotlin-classes/debug").get().asFile, excludes: fileFilter)
    def mainSrc = "$projectDir/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(dir: layout.buildDirectory.get().asFile, includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ]))
}

sonar {
    properties {
        property "sonar.projectKey", "xpeapp_android"
        property "sonar.projectName", "xpeapp_android"
        property "sonar.coverage.exclusions",
                "**/ui/**, \
                **/AppModule.kt,\
                **/MainActivity.kt,\
                **/XpeApp.kt,\
                **/DatastorePref.kt,\
                **/FirebaseService.kt,\
                **/WordpressService.kt,\
                **/*Interceptor.kt,"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml"
    }
}

dependencies {
    implementation('org.jetbrains.kotlin:kotlin-stdlib:2.2.0')
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.9.4'
    implementation 'androidx.activity:activity-compose:1.11.0'
    implementation "androidx.compose.ui:ui:1.9.2"
    implementation "androidx.compose.ui:ui-tooling-preview:1.9.2"
    implementation('androidx.compose.material3:material3-android:1.4.0')
    implementation("androidx.core:core-splashscreen:1.0.1")
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.constraintlayout:constraintlayout-compose:1.1.1'
    implementation "androidx.compose.material:material-icons-core:1.7.3"
    implementation 'androidx.compose.material:material-icons-extended:1.7.8'
    implementation "androidx.compose.animation:animation-core:1.9.2"
    implementation 'androidx.fragment:fragment:1.8.9'

    // Test
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.8.6'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.14.5'

    // Android Test
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.9.2"

    // Debug implementation
    debugImplementation "androidx.compose.ui:ui-tooling:1.9.2"
    debugImplementation "androidx.compose.ui:ui-test-manifest:1.9.2"

    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:34.3.0')

    // Firebase
    implementation("com.google.firebase:firebase-auth-ktx:22.3.0")
    implementation 'com.google.firebase:firebase-firestore-ktx:25.1.4'
    implementation 'com.google.firebase:firebase-messaging:25.0.1'
    implementation 'com.google.firebase:firebase-storage-ktx:21.0.2'
    implementation "com.google.firebase:firebase-analytics"

    // Navigation
    implementation 'androidx.navigation:navigation-compose:2.9.5'

    // Use Json
    implementation 'com.google.code.gson:gson:2.13.2'

    //ViewModel
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.9.4'

    //Retrofit for http requests
    implementation 'com.squareup.retrofit2:retrofit:3.0.0'
    implementation 'com.squareup.retrofit2:converter-gson:3.0.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.1.0'
    implementation 'com.squareup.okhttp3:okhttp:5.1.0'

    //Local data storage
    implementation 'androidx.datastore:datastore-preferences:1.1.7'
    implementation "androidx.security:security-crypto:1.0.0"
    implementation "androidx.security:security-identity-credential:1.0.0-alpha03"

    //Notifications
    implementation('com.google.accompanist:accompanist-systemuicontroller:0.36.0')
    implementation('com.google.accompanist:accompanist-permissions:0.37.3')

    //XpehoUI
    implementation "com.xpeho.packages:xpeho_ui_android:1.7.0"
}